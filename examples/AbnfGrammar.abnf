; ABNF for ABNF (RFC5234, RFC7405)
; This is a simplified version for parser testing
rulelist    =  1*( rule / (*c-wsp c-nl) )
rule        =  rulename defined-as elements c-nl
rulename    =  ALPHA *(ALPHA / DIGIT / "-")
elements    =  alternation
alternation =  concatenation *("/" concatenation)
concatenation = repetition *(1*c-wsp repetition)
repetition  =  [repeat] element
repeat      =  1*DIGIT / ( 1*DIGIT "*" [1*DIGIT] )
element     =  rulename / group / option / char-val / num-val / prose-val
group       =  "(" *c-wsp alternation *c-wsp ")"
option      =  "[" *c-wsp alternation *c-wsp "]"
char-val    =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
num-val     =  "%" ( bin-val / dec-val / hex-val )
prose-val   =  "<" *(%x20-3D / %x3F-7E) ">"
c-wsp       =  WSP / (c-nl WSP)
c-nl        =  comment / CRLF
defined-as  =  "=" / "=/"
comment     =  ";" *(WSP / VCHAR) CRLF

ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z

         BIT            =  "0" / "1"

         CHAR           =  %x01-7F
                                ; any 7-bit US-ASCII character,
                                ;  excluding NUL




         CR             =  %x0D
                                ; carriage return

         CRLF           =  CR LF
                                ; Internet standard newline

         CTL            =  %x00-1F / %x7F
                                ; controls

         DIGIT          =  %x30-39
                                ; 0-9

         DQUOTE         =  %x22
                                ; " (Double Quote)

         HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

         HTAB           =  %x09
                                ; horizontal tab

         LF             =  %x0A
                                ; linefeed

         LWSP           =  *(WSP / CRLF WSP)
                                ; Use of this linear-white-space rule
                                ;  permits lines containing only white
                                ;  space that are no longer legal in
                                ;  mail headers and have caused
                                ;  interoperability problems in other
                                ;  contexts.
                                ; Do not use when defining mail
                                ;  headers and use with caution in
                                ;  other contexts.

         OCTET          =  %x00-FF
                                ; 8 bits of data

         SP             =  %x20

         VCHAR          =  %x21-7E
                                ; visible (printing) characters

         WSP            =  SP / HTAB
                                ; white space
